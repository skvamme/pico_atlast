( Draw 7-seg sixel graphics in a terminal emulator, Xterm )
( Type RESET and then turn OFF sixel scrolling in middle mouse button menu in Xterm )
( CTRL+middle_mouse_button )
( Type RUN )
( Type MOUSE to activate the mouse function )

15 string pos
4 string currentcolor

: black "#0" currentcolor strcpy ;
: red "#1" currentcolor strcpy ;
: green "#2" currentcolor strcpy ;
: blue "#3" currentcolor strcpy ;
: white "#4" currentcolor strcpy ;
: reset 27 emit "c" type ;
: mouse 27 emit 91 emit "?1000h" type ; ( DECSET CSI ? Pm h )
: klick 2 pick 3 = if  drop drop drop  ." " Row " . ." " Col ". ." " Button " . cr then ;  
: noscroll 27 emit 91 emit "?80l" type ; ( disable sixel scrolling, will change to ?80h in xterm 369 )
: scroll 27 emit 91 emit "?80h" type ;   ( ?80l in xterm <= 369 )
: color "#0;2;0;0;0#1;2;100;0;0#2;2;0;100;00#3;2;0;0;100#4;2;100;100;100" 
   type currentcolor type ;
: end 27 emit 92 emit ;
: cr_ "$" type ;
: crlf  "-" type ;
: left "!9?" pos strcpy ;
: right "!49?" pos strcpy ;
: pad 10 1 do crlf loop ; ( top margin )
: hi ;
: mid 8 1 do crlf loop ;
: lo 15 1 do crlf loop ;
: start 27 emit "Pq" type color pad ;

: horizontal "!17?_ow{}!26~}{wo_" type crlf "!17?@BFN^!26~^NFB@" type crlf ;
: vertical crlf pos type "!2?_ow{{wo_" type crlf pos type 6 1 do "!12~" 
	type crlf pos type loop "!2?@BFNNFB@" type crlf ;

: wipe start 17 1 do "!61~" type crlf loop end ; 

: one start hi right vertical end start mid right vertical end ;

: two start hi horizontal end start hi right vertical end start mid horizontal end 
	start mid left vertical end start lo horizontal end ;

: three one start hi horizontal end start mid horizontal end start lo horizontal end ;

: four one start hi left vertical end start mid horizontal end ;

: five start hi horizontal end start hi left vertical end start mid horizontal end 
	start mid right vertical end start lo horizontal end ;

: six five start mid left vertical end ;

: seven one start hi horizontal end ;

: nine four start hi horizontal end ;

: zero one start hi horizontal end start hi left vertical end 
	start mid left vertical end start lo horizontal end ;

: eight zero start mid horizontal end ;

: run noscroll red zero 1 longsleep black zero
blue one 1 longsleep black one
green two  1 longsleep black two
red three  1 longsleep black three
blue four  1 longsleep black four  
green five  1 longsleep black five
red six  1 longsleep black six
blue seven  1 longsleep black seven
green eight  1 longsleep black eight
red nine  1 longsleep black nine
blue zero ;